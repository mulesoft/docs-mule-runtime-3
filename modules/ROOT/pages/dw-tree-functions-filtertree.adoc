= filterTree
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]



[[filtertree1]]
== filterTree&#40;value: Any, criteria: &#40;value: Any, path: Path&#41; &#45;&#62; Boolean&#41;: Any

Go through the nodes of the value and allows to filter them.
The `criteria` is going to be called with each value and its path.
If it returns true the node will remain if false it will be filtered.

_Introduced in DataWeave 2.4.0. Supported by Mule 4.4.0 and later._
=== Parameters

[%header, cols="1,3"]
|===
| Name   | Description
| value | The value to be filtered
| criteria | The expression that will determine if the node is filtered or not.
|===

=== Example

This example shows how the `filterTree` behaves under different inputs.

==== Source

[source,DataWeave,linenums]
----
%dw 2.0
import * from dw::util::Tree
output application/json
---
  {
    a: {name : "", lastName @(foo: ""): "Achaval", friends @(id: 123): [{id: "", test: true}, {age: 123}, ""]} filterTree ((value, path) ->
      value match  {
        case s is String -> !isEmpty(s)
        else -> true
      }
    ),
    b: null filterTree ((value, path) -> value is String),
    c: [{name: "Mariano", friends: []}, {test: [1,2,3]}, {dw: ""}] filterTree ((value, path) -> value match  {
      case a is Array ->  !isEmpty(a as Array)
      else -> true
    }),

  }

----

==== Output

[source,DataWeave,linenums]
----
{
   a: {
     lastName: "Achaval",
     friends @(id: 123): [
       {
         test: true
       },
       {
         age: 123
       }
     ]
   },
   b: null,
   c: [
     {
       name: "Mariano"
     },
     {
       test: [
         1,
         2,
         3
       ]
     },
     {
       dw: ""
     }
   ]
 }
----

